buildscript {
	repositories {
		jcenter()
		maven { url = "http://files.minecraftforge.net/maven" }
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'

repositories {
	maven {
		name = "CurseForge"
		url = "https://minecraft.curseforge.com/api/maven/"
	}
	maven {
		// CraftTweaker
		name = "blamejared"
		url = "http://maven.blamejared.com/"
	}
	maven {
		// General
		name = "mcmoddev"
		url = "https://maven.mcmoddev.com"
	}
	maven {
		// Fallbacks
		name = "ModMaven"
		url = "https://modmaven.k-4u.nl"
	}
	maven {
		url = "https://www.cursemaven.com"
	}
}

version = "${mod_version}-${mc_version}"
group = "trinity"
archivesBaseName = "Trinity"
sourceCompatibility = targetCompatibility = '1.8'
compileJava {
	sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
	version = "${mc_version}-${forge_version}"
	runDir = "run"
	replace "@VERSION@", "${mod_version}"
	mappings = "${mapping_version}"
	makeObfSourceJar = true
}

dependencies {
	// compile against the JEI API but do not include it at runtime
	deobfProvided "mezz.jei:jei_${mc_version}:${jei_version}:api"
	// at runtime, use the full JEI jar
	runtime "mezz.jei:jei_${mc_version}:${jei_version}"
	//deobfCompile "baubles:Baubles:${baubles_version}"
	deobfCompile ("curse.maven:cofh-core-69162:2920433") {
		transitive = false
	}
	deobfCompile "CraftTweaker2:CraftTweaker2-MC1120-Main:${crafttweaker_version}"
	//deobfCompile "nuclearcraft-mod:NuclearCraft:2.18k:1.12.2"
	compileOnly "thaumcraft:Thaumcraft:1.12.2:6.1.BETA26"
	deobfCompile "nuclearcraft-overhauled:NuclearCraft:2o.4.5:1.12.2"
	deobfCompile "curse.maven:icbm-classic-244451:2918052"
	deobfCompile "mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.28-17"
	deobfCompile "mekanism:Mekanism:${mekanism_version}"
	deobfCompile "li.cil.oc:OpenComputers:${oc_version}"

	deobfCompile "net.industrial-craft:industrialcraft-2:${ic2_version}:api"
}

processResources {
	exclude '**/Thumbs.db'
	
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}
	
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

task deobfJar(type: Jar) {
	from sourceSets.main.output
	classifier = 'deobf'
}

artifacts {
	archives deobfJar
}
